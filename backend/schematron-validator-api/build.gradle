group 'org.elifesciences'
version currentVersion

buildscript {
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
        mavenCentral()
    }

    dependencies {
        classpath 'org.springframework.build.gradle:propdeps-plugin:0.0.5'
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE")
    }
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'propdeps'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

processResources {
    from (project.rootDir.parent + "/vendor/elife/reference-schematron") {
        into 'reference-schematron'
    }
}

jar {
    baseName = 'elife-schematron-validator'
    version = '0.1.0'
}

bootRepackage {
    executable = true
}

dependencies {
    optional group: 'org.springframework.boot', name: 'spring-boot-configuration-processor'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-jetty'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator'
    compile(group: 'org.springframework.boot', name: 'spring-boot-starter-web') {
        exclude module: "spring-boot-starter-tomcat"
    }

    compile project(':schematron-validator-common')
    compile group: 'com.google.guava', name: 'guava', version: '11.0.2'

    testCompile group: 'junit', name: 'junit'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
}

compileJava.dependsOn(processResources)
